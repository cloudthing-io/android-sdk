buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    group = 'io.cloudthing.sdk'
    version = '0.1.0-SNAPSHOT'

    ext {
        isLibrary = false
    }
}

subprojects {
    afterEvaluate {Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion 24
                buildToolsVersion "24.0.2"

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion 15
                    targetSdkVersion 24
                    versionCode 1
                    versionName "1.0"
                }
            }
        }

        if(project.isLibrary && rootProject.hasProperty('sonatypeUser') && rootProject.hasProperty('sonatypePassword')) {
            configure(project) {
                apply plugin: 'maven'
                apply plugin: 'signing'

                signing {
                    sign configurations.archives
                }

                uploadArchives {
                    repositories.mavenDeployer {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//                        repository(url: "file://$buildDir/repo") {
//
//                        }

                        repository(url: sonatypeRepo) {
                            authentication(userName: sonatypeUser , password: sonatypePassword)
                        }

                        snapshotRepository(url: sonatypeSnapshotRepo) {
                            authentication(userName: sonatypeUser , password: sonatypePassword)
                            snapshots(updatePolicy: 'always')
                        }

                        task sourcesJar(type: Jar) {
                            from android.sourceSets.main.java.srcDirs
                            classifier = 'sources'
                        }
                        task javadoc(type: Javadoc) {
                            source = android.sourceSets.main.java.srcDirs
                            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                        }
                        task javadocJar(type: Jar, dependsOn: javadoc) {
                            classifier = 'javadoc'
                            from javadoc.destinationDir
                        }

                        artifacts {
                            archives javadocJar, sourcesJar
                        }

                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/cloudthing-io/android-sdk'

                            scm {
                                url 'scm:git@github.com:cloudthing-io/android-sdk.git'
                                connection 'scm:git@github.com:cloudthing-io/android-sdk.git'
                                developerConnection 'scm:git@github.com:cloudthing-io/android-sdk.git'
                            }

                            licenses {
                                license {
                                    name 'The MIT License (MIT)'
                                    url 'http://opensource.org/licenses/MIT'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'krystyw'
                                    name 'Krzysztof Drożyński'
                                    email 'drozynski.krzysztof@gmail.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
